apply plugin: 'java'
apply plugin: 'com.google.protobuf'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'kotlin'



startScripts.enabled = false


repositories {
	mavenCentral()
}

buildscript {
    ext {
        kotlinVersion = '1.0.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.7'
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")

    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src/generated"

    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.0.0-beta-2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.14.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/generated/main/java'
    main.java.srcDirs += 'src/generated/main/grpc'


}



dependencies {
    compile 'io.grpc:grpc-all:0.14.0'
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")

}

task authServer(type: CreateStartScripts) {
    mainClassName = "io.kauthz.AuthServer"
    applicationName = "authServer"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}


task authClient(type: CreateStartScripts) {
    mainClassName = "io.kauthz.AuthClient"
    applicationName = "authClient"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}


applicationDistribution.into("bin") {

    from(authServer)
    from(authClient)

    fileMode = 0755
}


idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

